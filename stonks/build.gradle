import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.github.dockerjava.api.exception.NotFoundException

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'com.bmuschko.docker-remote-api' version '6.3.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.70'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"

    compile group: 'io.ktor', name: 'ktor-server-netty', version: '1.3.1'
    compile group: 'io.ktor', name: 'ktor-client-apache', version: '1.3.1'
    compile group: 'com.typesafe', name: 'config', version: '1.4.0'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.testcontainers', name: 'testcontainers', version: '1.13.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'exchange.MainKt'
    }
    from {
        configurations.runtimeClasspath.collect {
            if (it.isDirectory()) {
                it
            } else {
                zipTree(it)
            }
        }
    }
}

task removeImage(type: DockerRemoveImage) {
    targetImageId("kokorin/exchange:latest")
    onError { exception ->
        if (exception.class == NotFoundException.class) {
            project.logger.info('Trying to remove docker image, but no image found')
        } else {
            throw ex
        }
    }
}

task copyJar(type: Copy, dependsOn: [jar]) {
    from "build/libs/exchange-${project.version}.jar"
    into 'build/docker'
}

task createDockerfile(type: Dockerfile, dependsOn: [copyJar]) {
    from 'openjdk:8'
    addFile("exchange-${project.version}.jar", "/usr/src/jar/exchange-${project.version}.jar")
    defaultCommand("java", "-jar", "/usr/src/jar/exchange-${project.version}.jar")
}

task buildImage(type: DockerBuildImage, dependsOn: [createDockerfile, removeImage]) {
    images.add("kokorin/exchange:latest")
}

test.dependsOn buildImage
